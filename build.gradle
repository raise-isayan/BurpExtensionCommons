
plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'  
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    // Define the main class for the application.
    mainClass = 'BurpExtensionCommons.Main'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

clean.doFirst {
    delete fileTree('release') {
      include '*.jar'
    }
}

jar {
  destinationDirectory = file('release')
   archiveVersion =  "v${release_version_major}.${release_version_minor}"
}

shadowJar {
   destinationDirectory = file('release')
   archiveVersion =  "v${release_version_major}.${release_version_minor}"
}

task release(type: Zip, dependsOn: ['build']) {
    archiveBaseName ="${rootProject.name}_v${release_version_major}.${release_version_minor}"
    destinationDirectory = file("${projectDir}")
    from rootProject.rootDir
    include '*'
    include 'gradle/**'
    include 'src/**'
    include 'release/*.jar'
    exclude 'build'
    exclude '.git'
    exclude '.gradle'
    exclude '*.zip'
}

dependencies {

    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    compileOnly 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    compileOnly 'org.bouncycastle:bcprov-jdk18on:1.78.1'

    // https://mvnrepository.com/artifact/org.brotli/dec
    compileOnly 'org.brotli:dec:0.1.2'

    // https://mvnrepository.com/artifact/net.portswigger.burp.extensions/montoya-api
    implementation 'net.portswigger.burp.extensions:montoya-api:2025.6'
        // https://mvnrepository.com/artifact/com.google.code.gson/gson
        implementation 'com.google.code.gson:gson:2.13.1'
        // https://mvnrepository.com/artifact/com.googlecode.juniversalchardet/juniversalchardet
        implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    // Unit Test

    // https://mvnrepository.com/artifact/org.bouncycastle/bcpkix-jdk18on
    testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk18on
    testImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'

    // Use JUnit Jupiter for testing.
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.1'

    // https://mvnrepository.com/artifact/net.portswigger.burp.extensions/montoya-api
    testImplementation 'net.portswigger.burp.extensions:montoya-api:2025.6'

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation 'org.mockito:mockito-core:5.4.0'

}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
